#!ipxe
###
### netboot.xyz-custom menu example
###

:custom
clear custom_choice
menu Custom ISO installers
item --gap Windows
item win10 ${space} Windows 10
item win11 ${space} Windows 11

item --gap Linux Systems
item zorin_os_17_pro ${space} Zorin OS 17 Pro
item zorin_os_16_pro ${space} Zorin OS 16 Pro
item zorin_os_16_pro_lite ${space} Zorin OS 16 Pro Lite
item ubuntu_server ${space} Ubuntu Server 
item pfsense ${space} PFSense
item opnsense ${space} OPNSense
choose custom_choice || goto custom_exit
echo ${cls}
goto ${custom_choice}
goto custom_exit

:option_one
kernel http://path.to/vmlinuz
initrd http://path.to/initrd
imgargs vmlinuz put_kernel_img_args_here
boot || goto custom_exit

:option_two
kernel https://boot.netboot.xyz/memdisk raw iso
initrd http://path.to/iso
boot || goto custom_exit

:option_three
echo Chains into another menu...
chain https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/custom1.ipxe || goto custom

:win11
set os_root ${live_endpoint}/Windows11/Windows11.iso
cpuid --ext 29 && set arch amd64
kernel https://${boot_domain}/wimboot
initrd ${os_root}/media/Boot/BCD	BCD
initrd ${os_root}/media/Boot/boot.sdi	boot.sdi
initrd -n boot.wim ${os_root}/sources/boot.wim	boot.wim
boot

:win10
set os_root ${live_endpoint}/Windows10.iso
kernel https://${boot_domain}/wimboot gui
initrd ${os_root}/winpeshl.ini     winpeshl.ini
initrd ${os_root}/install.bat      install.bat
initrd ${os_root}/installs/windows/win10/boot/bcd         BCD
initrd ${os_root}/installs/windows/win10/boot/boot.sdi    boot.sdi
initrd ${os_root}/boot.wim boot.wim
#initrd ${os_root}/installs/windows/win10/sources/boot.wim boot.wim
boot

:win11_custom
#kernel http://${boot_domain}/memdisk iso
kernel http://${boot_domain}/wimboot
echo Fetched memdisk...
echo ${boot_domain}
#kernel http://${boot_domain}/wimboot
#initrd http://${boot-url}/Windows11.iso
initrd ${live_endpoint}/Windows11.iso
echo ${live_endpoint}
echo ...Booting Windows 10...
boot || goto custom_exit

:zorin_os_16_pro
#set kernel_url ${live_endpoint}/ubuntu-squash/releases/download/20.04.6-8618b61c/
set kernel_url ${live_endpoint}/Zorin-OS-16-Pro/
imgfree
set zorin_16_iso_url ${live_endpoint}/Zorin-OS-16-Pro/Zorin-OS-16.1-Pro-64-bit.iso
set netboot_params ip=dhcp netboot=url url=${zorin_16_iso_url}
kernel ${kernel_url}vmlinuz root=/dev/ram0 ramdisk_size=5771512 ${netboot_params} initrd=initrd.magic ${cmdline}
initrd ${kernel_url}initrd.lz4
boot

:zorin_os_16_pro_lite
#set kernel_url ${live_endpoint}/ubuntu-squash/releases/download/20.04.6-8618b61c/
set kernel_url ${live_endpoint}/Zorin-OS-16-Pro/
imgfree
set zorin_16_iso_url ${live_endpoint}/Zorin-OS-16-Pro/Zorin-OS-16.1-Pro-Lite-64-bit.iso
set netboot_params ip=dhcp netboot=url url=${zorin_16_iso_url}
kernel ${kernel_url}vmlinuz root=/dev/ram0 ramdisk_size=5771512 ${netboot_params} initrd=initrd.magic ${cmdline}
initrd ${kernel_url}initrd.lz4
boot

:zorin_os_17_pro
#set kernel_url ${live_endpoint}/ubuntu-squash/releases/download/17-71c713e6/
set kernel_url ${live_endpoint}/Zorin-OS-17-Pro/
imgfree
set zorin_iso_url ${live_endpoint}/Zorin-OS-17-Pro/Zorin-OS-17-Pro-64-bit.iso
set netboot_params ip=dhcp netboot=url url=${zorin_iso_url}
kernel ${kernel_url}vmlinuz root=/dev/ram0 ramdisk_size=6800000 ${netboot_params} initrd=initrd.magic ${cmdline}
initrd ${kernel_url}initrd.zstd
boot

:ubuntu_server
imgfree
set kernel_url ${live_endpoint}/ubuntu-squash/releases/download/22.04.3-7e94379b/
set ubuntu_iso_url ${live_endpoint}/ubuntu-22.04.3-live-server-amd64.iso
set netboot_params ip=dhcp url=${ubuntu_iso_url}
echo Loading Ubuntu Subiquity Network Installer...
kernel ${kernel_url}vmlinuz root=/dev/ram0 ramdisk_size=2100000 ${netboot_params} initrd=initrd.magic ${cmdline}
initrd ${kernel_url}initrd
boot

:custom_exit
chain utils.ipxe
#clear menu
exit 0
